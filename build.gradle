configurations{
    shade
    compile.extendsFrom shade
}

buildscript{
    repositories{
        mavenCentral()
        
        maven{
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        
        maven{
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    
    dependencies{
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

dependencies{
    shade files('libs/JavaCheckerShade  MC-UNIVERSAL  v1.2.jar')
}

apply plugin: 'forge'

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft{
    version = "1.7.10-10.13.4.1492-1.7.10"
    runDir = "eclipse"
    
    srgExtra "PK: chylex/javacheck chylex/shade/javacheck"
}

task createResourcePack(type: Zip){
    from "music"
    archiveName "HardcoreEnderExpansion  Music  v1.0.zip"
    destinationDir = file("build/libs")
}

String modid = ""
String modver = ""

processResources{
    File info = new File(sourceSets.main.resources.srcDirs.iterator().next(), "mcmod.info")
    def slurp = new groovy.json.JsonSlurper()
    def data = slurp.parseText(info.getText())[0]
    
    modid = data.modid
    modver = data.version
}

jar{
    archiveName = modid+"  MC-"+project.minecraft.version+"  v"+modver+".jar"
    
    configurations.shade.each{ dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**', 'mcmod.info'
        }
    }
    
    manifest{
        attributes 'FMLAT': 'hee_at.cfg'
        attributes 'TweakClass': 'chylex.shade.javacheck.Java8Checker'
    }
}

tasks.build.dependsOn createResourcePack
